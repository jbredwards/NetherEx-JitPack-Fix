buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

version = mod_version
group = "com.github.logictechcorp"
archivesBaseName = "${mod_name}-${minecraft_version}"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    version = "${minecraft_version}-${forge_version}"
    mappings = mcp_version
    runDir = "run"
    makeObfSourceJar = false
}

repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

dependencies {
    deobfCompile 'com.github.jbredwards:LibraryEx-JitPack-Fix:1.12.x-SNAPSHOT'
    compile 'com.electronwill.night-config:json:3.6.0'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
    archives deobfJar
    archives sourcesJar
}

jar {
    exclude "**/*.bat"
    
    manifest {
        attributes([
                'FMLAT': "netherex_at.cfg"
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact deobfJar
            artifact sourcesJar
        }
    }
}
